buildscript {
    dependencies {
        repositories {
            mavenCentral()
            // serve per org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1
            maven { url "https://plugins.gradle.org/m2/" }
            maven { url 'https://oss.jfrog.org/libs-snapshot' }
            maven { url 'https://dl.bintray.com/sysdata/maven' }
        }
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'

def libName = 'baseandroid-eventdispatcher'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode libVersionCode
        versionName libVersionName
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {

    def adapterVersionName = project.ext.libVersionName

    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    //log
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.github.tony19:logback-android-core:1.1.1-4'
    compile 'com.github.tony19:logback-android-classic:1.1.1-4'
    // otto
    compile 'com.squareup:otto:1.3.8'

    // BusAdapter
    compile "com.baseandroid:baseandroid-busadapter:$adapterVersionName"

    // rxJava
    compile 'io.reactivex:rxandroid:1.1.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.3'
}

// pubblicazione maven
if(project.rootProject.file('local.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    ext {
        // GROUP_ID
        publishedGroupId = 'com.baseandroid'
        // ARTIFACT_ID
        artifact = libName
        // VERSION_ID
        libraryVersion = libVersionName

        developerId = properties.getProperty("bintray.developer.id")
        developerName = properties.getProperty("bintray.developer.name")
        developerEmail = properties.getProperty("bintray.developer.email")

        bintrayRepo = 'maven'
        bintrayName = 'baseandroid-eventdispatcher'
        libraryName = 'baseandroid-eventdispatcher'
        bintrayOrganization = 'sysdata'
    }

    apply from: 'publishBintray.gradle'
}